#@TYPE: Machine
#@NAME: Variscite DART-MX95
#@SOC: i.MX95
#@DESCRIPTION: Machine configuration for Variscite DART-MX95
#@MAINTAINER: Francesco FErraro <francesco.f@variscite.com>

MACHINEOVERRIDES =. "mx95:"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/armv8-2a/tune-cortexa55.inc

require variscite.inc

# Variscite BSP default providers
PREFERRED_PROVIDER_virtual/kernel:imx95-var-dart ?= "linux-variscite"
PREFERRED_PROVIDER_u-boot:imx95-var-dart ?= "u-boot-variscite"
PREFERRED_PROVIDER_u-boot-fw-utils:imx95-var-dart ?= "u-boot-fw-utils"
PREFERRED_PROVIDER_virtual/bootloader:imx95-var-dart ?= "u-boot-variscite"

PREFERRED_VERSION_cortexm-toolchain-cross-armv8-m = "10.3-2021.10"

MACHINE_FEATURES += "nxpiw612-sdio"

KERNEL_DEVICETREE = " \
    freescale/imx95-var-dart-dt8mcustomboard.dtb \
"
UBOOT_DTB_NAME = "imx95-var-dart-dt8mcustomboard.dtb"

LOADADDR = ""
UBOOT_SUFFIX = "bin"
UBOOT_MAKE_TARGET = ""

SPL_BINARY = "spl/u-boot-spl.bin"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd]   = "imx95_var_dart_defconfig,sdcard"

# Set ATF platform name
ATF_PLATFORM = "imx95"
OEI_CONFIGS = "ddr tcm"
OEI_CORE   = "m33"
OEI_SOC    = "mx95"
OEI_BOARD  = "mx95lp5"
DDR_TYPE   = "lpddr5"

IMXBOOT_VARIANTS = ""
IMXBOOT_TARGETS_BASENAME = "flash"

# Multiple system manager configs by IMXBOOT_VARIANT
SYSTEM_MANAGER_CONFIG = "${@bb.utils.contains('IMXBOOT_VARIANT', 'alt',       'mx95alt', \
                            bb.utils.contains('IMXBOOT_VARIANT', 'jailhouse', 'mx95evkjailhouse', \
                            bb.utils.contains('IMXBOOT_VARIANT', 'netc',      'mx95netc', \
                                                                              'mx95evk', d), d), d)}"

# imx-boot (flash.bin) targets based on UBOOT_CONFIG and IMXBOOT_VARIANT
IMXBOOT_TARGETS_SD = "${@bb.utils.contains('IMXBOOT_VARIANT', 'alt',       '${IMXBOOT_TARGETS_BASENAME}_alt', \
                         bb.utils.contains('IMXBOOT_VARIANT', 'jailhouse', '${IMXBOOT_TARGETS_BASENAME}_jailhouse', \
                         bb.utils.contains('IMXBOOT_VARIANT', 'netc',      '${IMXBOOT_TARGETS_BASENAME}_netc', \
                                                                           '${IMXBOOT_TARGETS_BASENAME}_all ${IMXBOOT_TARGETS_BASENAME}_a55', d), d), d)} \
"

IMXBOOT_TARGETS = "${IMXBOOT_TARGETS_SD}"

IMX_BOOT_SOC_TARGET = "iMX95"
IMX_BOOT_SEEK = "32"

# We have to disable SERIAL_CONSOLE due to auto-serial-console
SERIAL_CONSOLES = "115200;ttyLP0"

DDR_FIRMWARE_NAME = " \
    lpddr5_dmem_v202311.bin \
    lpddr5_dmem_qb_v202311.bin \
    lpddr5_imem_v202311.bin \
    lpddr5_imem_qb_v202311.bin \
"

M4_DEFAULT_IMAGE_MX95 = "imx95-19x19-evk_m7_TCM_power_mode_switch_rtos_imx95_sm_cm7.bin"

IMAGE_BOOT_FILES:append = " \
    imx95-19x19-evk_m7_TCM_low_power_wakeword_sm_cm7.bin \
    imx95-19x19-evk_m7_TCM_power_mode_switch_rtos_imx95_sm_cm7.bin \
    imx95-19x19-evk_m7_TCM_rpmsg_lite_pingpong_rtos_linux_remote_sm_cm7.bin \
    imx95-19x19-evk_m7_TCM_rpmsg_lite_str_echo_rtos_sm_cm7.bin \
"
